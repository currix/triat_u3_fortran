      PROGRAM ENRGY_MODH_U3
C
C     PROGRAM TO COMPUTE THE ENERGIES FOR THE U(3) MODEL HAMILTONIAN
C     TRIDIAGONAL MATRIX DIAGONALIZATION CASE
C
C                    by Currix TM
C     
      IMPLICIT NONE
C
C     DEFINITION OF PARAMETERS
C
      INTEGER N2MAX, DMAX, NPMAX, DIMSCR
C     
C     MAXIMUM NUMBER OF U(3) BOSONS (N_2)  
      PARAMETER (N2MAX = 40000) 
C
C     MAXIMUM DIMENSIONS OF VECTORS
      PARAMETER (DMAX = 20001)
C     DIMENSION OF SCRATCH VECTOR (5 MAXDIM)
      PARAMETER (DIMSCR = 100005) 
C     MAXIMUM NUMBER OF PARAMETERS IN THE HAMILTONIAN
      PARAMETER (NPMAX = 2)
C     
C     DEFINITION OF VARIABLES
C
C     REPRESENTATION OF U(3)
      INTEGER N2
C
C     VIBRATIONAL ANGULAR MOMENTUM
      INTEGER LVAL
C     OPTIONS:
C     IOPTS = 0 OUTPUTS ONLY GROUND STATE ENERGY
C     IOPTS = 1 OUTPUTS ALL ENERGIES
C     IOPTS = 2 OUTPUTS ALL ENERGIES WITH G.S. ENERGY = 0
C     IOPTS >= 3 OUTPUTS ENERGIES FOR EIGENSTATES 1 - IOPTS
      INTEGER IOPTS
C
C
C     HAMILTONIAN MATRIX
C
C HAMD = DIAGONAL HAML = LOWER/UPPER DIAGONAL
      DOUBLE PRECISION HAMD(DMAX)
      DOUBLE PRECISION HAML(DMAX)
C
C     COMPUTED ENERGIES MATRIX
      DOUBLE PRECISION EIGEN(DMAX)
C
C     SCRATCH VECTORS AND VARIABLES FOR DIAGONALIZATION
C     VECTORS IN COMMON
      DOUBLE PRECISION FV1
      COMMON/SCRATCH/ FV1(DIMSCR)
      INTEGER IV1(DIMSCR)
      INTEGER IERR
C
C     HAMILTONIAN PARAMETERS
C
C     ALGEBRAIC MODEL
C     PARAMETERS 
C
      DOUBLE PRECISION SCALE, XI
      DOUBLE PRECISION EPSILON, A
C
C     COMMON BLOCK FOR HAMILTONIAN PARAMETERS
C
      DOUBLE PRECISION HPAR
C
C    
      COMMON/HAMPAR/ HPAR(NPMAX)
C     
C     CONTROL OF DISPLAYED OUTPUT
      INTEGER IPRINT
C
C     CONTROL OUTPUT DISPLAYED
      COMMON/GRAF/ IPRINT
C
C     LOCAL VARIABLES                                                      
      INTEGER I,L,DIM
      DOUBLE PRECISION  EMIN
C
C     LAPACK SUBROUTINE VARIABLES
      DOUBLE PRECISION VL, VU
      INTEGER IL, IU, DIMOUT
      DOUBLE PRECISION ABSTOL, DLAMCH
      EXTERNAL DLAMCH
C
C     PARAMETER INPUT
      READ(5,*) N2 
      READ(5,*) LVAL
      READ(5,*) IOPTS
      READ(5,*) SCALE
      READ(5,*) XI
cc
c     Debugging variable
c     Minimum output
      IPRINT = 0
c     Saves Hamiltonian matrix
cc      iprint = -1
cc      write(*,*) iprint
cc
C
      IF (IPRINT.GT.2) WRITE(*,*) 'MAIN PROGRAM'
C
C
C     TESTS 
C    
      IF (LVAL.GT.N2) STOP 'LMX > N2, SAYONARA '
C
      IF (N2.GT.N2MAX) STOP 'N2 > N2MAX, SAYONARA '
C
C
      EPSILON = SCALE*(1.0D0-XI)
      A = SCALE*XI/DFLOAT(N2-1)
C     
      HPAR(1) = EPSILON
      HPAR(2) = A
C     
C     INITIALIZE EMIN
      EMIN = 0.0D0
C     
C     MAIN LOOP
C     
      L = LVAL
C     
      IF (IPRINT.GT.3) THEN
         WRITE(*,*)
         WRITE(*,*) 'L',L
         WRITE(*,*)
      ENDIF
C     BUILD ALGEBRAIC HAMILTONIAN
      CALL HAMU3TRID(N2, L, HAMD, HAML)  
C     
C     DIAGONALIZE HAMILTONIAN MATRIX
      DIM = (N2-MOD(N2-L,2)-L)/2+1
C
      IF (IOPTS.GT.DIM) STOP 'ERROR :: IOPTS > DIM. SAYONARA BABY' 
C
C     DIAGONALIZATION
C
      ABSTOL = 2*DLAMCH('S')
C
      IF (IOPTS.EQ.0) THEN
C     ONLY GROUND STATE ENERGY
         IL = 1
         IU = 1
         CALL DSTEVX( 'N', 'I', DIM, HAMD, HAML, VL, VU, IL, IU, ABSTOL, 
     *        DIMOUT, EIGEN, FV1, DIMSCR, FV1, IV1, IV1, IERR )
      ELSE IF (IOPTS.GE.3) THEN
C     FIRST IOPTS EIGENVALUES
         IL = 1
         IU = IOPTS
         CALL DSTEVX( 'N', 'I', DIM, HAMD, HAML, VL, VU, IL, IU, ABSTOL, 
     *        DIMOUT, EIGEN, FV1, DIMSCR, FV1, IV1, IV1, IERR )
      ELSE 
C     ALL EIGENVALUES 
         CALL DSTEVX( 'N', 'A', DIM, HAMD, HAML, VL, VU, IL, IU, ABSTOL, 
     *        DIMOUT, EIGEN, FV1, DIMSCR, FV1, IV1, IV1, IERR )
      ENDIF
C
C     CHECK IERR VALUE IN DSTEVX
C         WRITE(*,*) 'IERR VALUE ON OUTPUT OF DSTEVX = ', IERR
         IF (IERR.LT.0) WRITE(*,*) 'ERROR :: THE ', -IERR, '-TH ARGUMENT HAS
     * AN ILLEGAL VALUE'
C     
C     REFER ENERGIES TO G.S.
      EMIN = 0.0D0
      IF (IOPTS.EQ.2) EMIN = EIGEN(1)
C     
      DIMOUT = DIM
      IF (IOPTS.EQ.0) THEN
         DIMOUT = 1
      ELSE IF (IOPTS.GT.2) THEN
         DIMOUT = IOPTS
      ENDIF
C     
      DO I = 1, DIMOUT
         WRITE(*,*) EIGEN(I) - EMIN
      ENDDO
C
      END


