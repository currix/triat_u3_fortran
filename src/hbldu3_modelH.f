      SUBROUTINE HBLDU3MOD(N2,L,LD,HAM,W2MAT)
C     
C     SUBROUTINE THAT BUILDS THE MODEL HAMILTONIAN MATRIX IN  
C     THE U(3) MODEL FOR BENDING VIBRATIONS
C
C     INPUT
C     N2   : U(3) IRREP LABEL (BENDING)
C     L    : VIBRATIONAL ANGULAR MOMENTUM LABEL
C     LD   : LEADING DIMENSION OF HAM AND BL
C
C     OUTPUT
C     HAM     : HAMILTONIAN MATRIX
C     W2MAT   : ARRAY WITH THE SO(3) CASIMIR W^2 
C                             NOTE THAT THE PAIRING IS P = (N(N+1)-W^2
C     
C     BENDING HAMILTONIAN
C     TRIATOMIC ABC MOLECULE
C     G = C1 (BENT) OR C_INFV (LINEAR)
C
C     H = SCALE[(1-XI) n + XI/(N-1) P] 
C     H = EPSILON n + A P 
C
C     HPAR(1) = EPSILON
C     HPAR(2) = A
C     
C     by Currix TM
C     
      IMPLICIT NONE
C
      INTEGER NPMAX
C     MAXIMUM NUMBER OF PARAMETERS IN THE HAMILTONIAN
      PARAMETER (NPMAX = 2)
C     
C     DEFINITION OF VARIABLES
C     
      INTEGER N2, L, LD
C
      DOUBLE PRECISION HAM(LD,*)
      DOUBLE PRECISION W2MAT(LD,*)
C
C     COMMON BLOCK FOR HAMILTONIAN PARAMETERS
C
      DOUBLE PRECISION HPAR
C
C
      COMMON/HAMPAR/ HPAR(NPMAX)
C     
      INTEGER IPRINT
C
C     CONTROL OUTPUT DISPLAYED
      COMMON/GRAF/ IPRINT
C
C     LOCAL VARIABLES
      INTEGER I, J, I1, DIM
C     TEMPORAL VALUES TO STORE INTEGERS AS DOUBLE PREC. REALS
      DOUBLE PRECISION V2
C     
C     
      IF (IPRINT.GT.2) WRITE(*,*) 'SUBROUTINE HAMILTONIAN BUILDING'
C
C     MATRIX DIMENSIONS
      DIM = (N2-MOD(N2-L,2)-L)/2+1
C     INITIALIZATION
      DO I = 1, DIM
         DO J = 1, DIM
            HAM(J,I) = 0.0D0
         ENDDO
      ENDDO
C     
C     NON-DIAGONAL INTERACTIONS
C
C     BUILDING W2 BLOCK
C
      CALL SO3CASBUILD(LD,W2MAT,N2,L)
C
C     PAIRING INTERACTION :: P = N(N+1) - W^2
      DO I1 = 1, DIM
         HAM(I1,I1) = HPAR(2)*(DFLOAT(N2*(N2+1))-W2MAT(I1,I1))
      ENDDO
      DO I1 = 1, DIM-1
         HAM(I1+1,I1) = -HPAR(2)*W2MAT(I1+1,I1)
         HAM(I1,I1+1) = -HPAR(2)*W2MAT(I1,I1+1)
      ENDDO
C
C     DIAGONAL INTERACTIONS
C     
      DO I = 1, DIM
C
     
         V2 = DFLOAT(N2 - (2*I-2+MOD(N2-L,2)))
C     
         HAM(I,I) = HAM(I,I) + HPAR(1)*V2 
C     
      ENDDO
C     
C
      RETURN
      END
      









