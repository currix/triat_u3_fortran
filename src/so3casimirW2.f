      SUBROUTINE SO3CASBUILD(DIMBMAX,W2BLOCK,NN,L)
C
C     SUBROUTINE THAT BUILD THE SO(3) CASIMIR W^2 MATRIX IN
C     THE CYLINDRICAL OSCILLATOR BASIS. 
C     SO(3) = {L,D+,D-}
C
C     PROGRAM OF THE U(3) ALGEBRAIC MODEL FOR BENDING DYNAMICS
C     
C     INPUT 
C     DIMBMAX   : LEADING DIMENSION OF W2BLOCK
C     NN        : U(3) REPRESENTATION (TOTAL NUMBER OF U(3) BOSONS)
C     L         : VIBRATIONAL ANGULAR MOMENTUM
C
C     OUTPUT
C     W2BLOCK : LOCAL BASIS MATRIX
C     
C
C     
C     by Currix TM
C
C     
      IMPLICIT NONE
C     
C     DEFINITION OF VARIABLES
C     
      INTEGER DIMBMAX, NN, L
      DOUBLE PRECISION W2BLOCK(DIMBMAX,*)
C
      INTEGER IPRINT
C
C     CONTROL OUTPUT DISPLAYED
      COMMON/GRAF/ IPRINT
C
C     TEMPORAL VARIABLES                                     
      INTEGER I1, I2, DIMB
      DOUBLE PRECISION VN2, VNN, VL, VTMP
C
      IF (IPRINT.GT.2) WRITE(*,*) 'SUBROUTINE SO3CASBUILD STARTS HERE'
C
C
      DIMB = (NN - MOD(NN-L,2) - L)/2 + 1
C
C     INITIALIZING
C
      DO I1 = 1, DIMB
         DO I2 = 1, DIMB 
            W2BLOCK(I2,I1) = 0.0D0
         ENDDO
      ENDDO
C
      VNN = DFLOAT(NN)
      VL = DFLOAT(L)
C
C     NON-DIAGONAL PART
      DO I1 = 1, DIMB-1
         VN2 = DFLOAT(NN - MOD(NN-L,2) - 2*(I1-1))
         VTMP =
     *       DSQRT((VNN-VN2+2.0D0)*(VNN-VN2+1.0D0)*(VN2+VL)*(VN2-VL))
         W2BLOCK(I1+1,I1) = -VTMP
         W2BLOCK(I1,I1+1) = -VTMP
      ENDDO
C
C     DIAGONAL PART
      DO I1 = 1, DIMB                     
         VN2 = DFLOAT(NN - MOD(NN-L,2) - 2*(I1-1))
         W2BLOCK(I1,I1)=(VNN-VN2)*(VN2+2.0D0)+(VNN-VN2+1.0D0)*VN2+VL*VL
      ENDDO
C
      IF (IPRINT.GT.2) WRITE(*,*) 'SUBROUTINE SO3CASBUILD ENDS HERE'
C
      RETURN
      END







