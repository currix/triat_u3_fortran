      PROGRAM ENRGY_U3
C
C     PROGRAM TO COMPUTE THE ENERGIES FOR THE U(3) MODEL HAMILTONIAN
C
C                    by Currix TM
C     
      IMPLICIT NONE
C
C     DEFINITION OF PARAMETERS
C
      INTEGER N2MAX, DMAX, NPMAX, DIMSCR
C     
C     MAXIMUM NUMBER OF U(3) BOSONS (N_2)  
      PARAMETER (N2MAX = 6000) 
C     DIMENSION OF SCRATCH VECTOR
      PARAMETER (DIMSCR = 18000) 
C     MAXIMUM DIMENSIONS OF MATRICES
      PARAMETER (DMAX = 3001)
C     MAXIMUM NUMBER OF PARAMETERS IN THE HAMILTONIAN
      PARAMETER (NPMAX = 2)
C     
C     DEFINITION OF VARIABLES
C
C     REPRESENTATION OF U(3)
      INTEGER N2
C
C     VIBRATIONAL ANGULAR MOMENTUM
      INTEGER LVAL
C     OPTIONS:
C     IOPTS = 0 OUTPUTS ONLY GROUND STATE ENERGY
C     IOPTS = 1 OUTPUTS ALL ENERGIES
C     IOPTS >= 2 OUTPUTS ALL ENERGIES WITH G.S. ENERGY = 0
C     IOPTS = 3 OUTPUTS ALSO ENERGY DIFFERENCES
      INTEGER IOPTS
CC     LOGICAL EMINL
C
C     HAMILTONIAN MATRIX
      DOUBLE PRECISION HAM(DMAX,DMAX)
C
C     COMPUTED ENERGIES MATRIX
      DOUBLE PRECISION EIGEN(DMAX)
C     COMPUTED W2 (SO(3) CASIMIR) BLOCKS
      DOUBLE PRECISION W2MAT(DMAX,DMAX)
C
C     SCRATCH VECTORS AND VARIABLES FOR DIAGONALIZATION
C     VECTORS IN COMMON
      DOUBLE PRECISION FV1
      COMMON/SCRATCH/ FV1(DIMSCR)
      INTEGER IERR
C
C     HAMILTONIAN PARAMETERS
C
C     ALGEBRAIC MODEL
C     PARAMETERS 
C
      DOUBLE PRECISION SCALE, XI
      DOUBLE PRECISION EPSILON, A
C
C     COMMON BLOCK FOR HAMILTONIAN PARAMETERS
C
      DOUBLE PRECISION HPAR
C
C    
      COMMON/HAMPAR/ HPAR(NPMAX)
C     
C     CONTROL OF DISPLAYED OUTPUT
      INTEGER IPRINT
C
C     CONTROL OUTPUT DISPLAYED
      COMMON/GRAF/ IPRINT
C
C     LOCAL VARIABLES                                                      
      INTEGER I,L,DIM
c     integer j
      DOUBLE PRECISION  EMIN
C
C     PARAMETER INPUT
      READ(5,*) N2 
      READ(5,*) LVAL
      READ(5,*) IOPTS
      READ(5,*) SCALE
      READ(5,*) XI
cc
c     Debugging variable
c     Minimum output
      IPRINT = 0
c     Saves Hamiltonian matrix
cc      iprint = -1
cc      write(*,*) iprint
cc
C
      IF (IPRINT.GT.2) WRITE(*,*) 'MAIN PROGRAM'
C
C
C     TESTS 
C    
      IF (LVAL.GT.N2) STOP 'LMX > N2, SAYONARA '
      IF (N2.GT.N2MAX) STOP 'N2 > N2MAX, SAYONARA '
C
C
      EPSILON = SCALE*(1.0D0-XI)
      A = SCALE*XI/DFLOAT(N2-1)
C     
      HPAR(1) = EPSILON
      HPAR(2) = A
C     
C     INITIALIZE EMIN
      EMIN = 0.0D0
C     
C     MAIN LOOP
C     
      L = LVAL
C     
      IF (IPRINT.GT.3) THEN
         WRITE(*,*)
         WRITE(*,*) 'L',L
         WRITE(*,*)
      ENDIF
C     BUILD HAMILTONIAN MATRIX
C     ALGEBRAIC HAMILTONIAN
      CALL HBLDU3MOD(N2,L,DMAX,HAM,W2MAT)  
C     
C     DIAGONALIZE HAMILTONIAN MATRIX
      DIM = (N2-MOD(N2-L,2)-L)/2+1
C
C     DIAGONALIZATION
      CALL DSYEV('N', 'U', DIM, HAM, DMAX, EIGEN,  FV1,  3*DIM,
     *     IERR)
C     
C     REFER ENERGIES TO G.S.
      EMIN = 0.0D0
      IF (IOPTS.GE.2) EMIN = EIGEN(1)
C     
      IF (IOPTS.EQ.0) THEN
         WRITE(*,*) EIGEN(1)
      ELSE         
         DO I = 1, DIM
            WRITE(*,*) EIGEN(I) - EMIN
         ENDDO
C
         IF (IOPTS.EQ.3) THEN
            WRITE(*,*) '####################################'
C     
            DO I = 1, DIM-1
               WRITE(*,*) I, EIGEN(I+1) - EIGEN(I)
            ENDDO
         ENDIF
      ENDIF
C     
C     
      END
      
      



